Program:
    keypressDelay=KeypressDelay?
    colorOffset=ColorOffset?
    functions*=Function
    'Main {'
        commands+=Command
    '};'
;

ColorOffset:
    'colorOffset' '=' offset=INT';'
;

KeypressDelay:
    'keypressDelay' '=' keypress_delay=INT';'
;

Parameter:
    name=ID ':' type=Type
;

Attribute:
     int=INT | bool=BOOL | parameter=[Parameter]
;

Function:
    name=ID  '(' parameters*=Parameter[','] ')' ':' sequence+=SequencePart[','] ';'
;

Macro:
    keys*=Key['+']
;

Comparison:
    left=Attribute operator=Comparator right=Attribute
;

Condition:
    comparison=Comparison | identifier=[Parameter]
;

ElseStatement:
    'else' ':' '[' else_sequence+=SequencePart[','] ']'
;

IfCommand:
    '{' 'if(' condition=Condition ')'':' '[' if_sequence+=SequencePart[','] ']' ';' else_statement=ElseStatement? '}'
;

IfCursorCommand:
    '{' 'ifCursor(' color=Color ')'':' '[' if_sequence+=SequencePart[','] ']''}'
;

RandomSleep:
    'randomSleep(' min=Attribute ','  max=Attribute ')'
;

Sleep:
    'sleep(' sleepValue=Attribute ')'
;

SleepCommand:
    Sleep|RandomSleep
;

LoopCommand:
    'loop(' loop_iterator=Attribute ',' '['sequence+=SequencePart[','] ']'')'
;

SequencePart:
    if_cursor=IfCursorCommand | if_=IfCommand |loop=LoopCommand |sleep=SleepCommand | func_call=[Function] '(' attributes*=Attribute[','] ')' | macro=Macro
;

MacroCommand:
    macro=Macro ':' sequence+=SequencePart[','] ';'
;

AutoPixelColorCommand:
    'onColor(' color=Color ',' x=INT ',' y=INT ')' ':' sequence+=SequencePart[','] ';'
;

Command:
    auto_pixel_command=AutoPixelColorCommand | macro_command=MacroCommand
;

Key:
    /rightClick|leftClick|[cC]trl|[aA]lt|[Tt]ab|[Ss]hift|[a-zA-ZŠĐĆČŽšđćčž]|F[1-9]|F1[0-2]|[0-9]/
;

Color:
    /#[A-Fa-f0-9]{6}/
;

Type:
    'number'|'boolean'
;

Comparator:
    '<' | '>' | '<=' | '>=' | '==' | '!='
;